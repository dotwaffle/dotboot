#!ipxe

set boot-url http://boot.on6.co.uk
set bootstrap-url ${boot-url}/bootstrap
set menu-url ${boot-url}/menu.ipxe

# Boot <boot-url>/bootstrap/hostname-<hostname>.ipxe
# if hostname DHCP variable is set and script is present
isset ${hostname} && chain --replace --autofree ${bootstrap-url}/hostname-${hostname}.ipxe && exit ||

# Boot <boot-url>/bootstrap/uuid-<UUID>.ipxe
# if uuid SMBIOS variable is set and script is present
isset ${uuid} && chain --replace --autofree ${bootstrap-url}/uuid-${uuid}.ipxe && exit ||

# Boot <boot-url>/bootstrap/mac-01-02-03-04-05-06.ipxe if script is present
chain --replace --autofree ${bootstrap-url}/mac-${mac:hexhyp}.ipxe && exit ||

# Boot <boot-url>/bootstrap/bus-01-80-86-10-0e.ipxe if PCI Intel adapter
# is present and script is present
chain --replace --autofree ${bootstrap-url}/bus-${busid:hexhyp}.ipxe && exit ||

set menu-timeout 5000
set submenu-timeout ${menu-timeout}

# Ensure we have menu-default set to something
isset ${menu-default} || set menu-default exit

# If we don't have a hostname, let's set one
isset ${hostname} || set hostname machine

:start
menu iPXE boot menu for ${hostname} at ${boot-url}
item --gap --             ------------------------- Operating systems ------------------------------
item --key d menu-debian  Install Debian...
item --key u menu-ubuntu  Install Ubuntu...
item --key f menu-fedora  Install Fedora...
item --gap --             ------------------------- Tools and utilities ----------------------------
item --gap --             ------------------------- Advanced options -------------------------------
item --key c config       Configure settings
item shell                Drop to iPXE shell
item reboot               Reboot computer
item
item --key x exit         Exit iPXE and continue BIOS boot
choose --timeout ${menu-timeout} --default ${menu-default} selected || goto cancel
set menu-timeout 0
goto ${selected}

:cancel
echo You cancelled the menu, dropping you to a shell

:shell
echo Type exit to get the back to the menu
shell
set menu-timeout 0
set submenu-timeout 0
goto start

:failed
echo Booting failed, dropping to shell
goto shell

:reboot
reboot

:exit
exit

:config
config
goto start

:back
set submenu-timeout 0
clear submenu-default
goto start
